apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://twitter4j.org/maven2"
    }
}

class GitUtil {
    def directory

    def getRevHash() {
        def proc = ["git", "rev-parse", "HEAD"].execute(null, directory)
        proc.waitFor()
        return proc.inputStream.readLines().first()
    }

    def getRevCount() {
        def proc = ["git", "rev-list", "--count", "HEAD"].execute(null, directory)
        proc.waitFor()
        return proc.inputStream.readLines().first()
    }

    def isDirty() {
        def proc = ["git", "status", "--porcelain"].execute(null, directory)
        proc.waitFor()
        return proc.inputStream.readLines().size() > 0
    }
}

ext {
    playServicesVersion = '17.6.0'
    twitter4jVersion = '4.0.7'
    mastodon4jVersion = '087b09001e99942c3423bfd6f4641c8a3a5560ce'

    y4aVersionMajor = 3
    y4aVersionMinor = 2
    y4aVersionPatch = 0
    y4aVersionSuffix = ''
    y4aCodeName = 'memento'

    git = new GitUtil(directory: rootProject.projectDir)
    y4aRevision = git.revHash.substring(0, 7)
    if (git.isDirty()) {
        y4aRevision += "*"
    }
    y4aBuildDate = new Date().format("yyMMdd")
    y4aVersionString = [y4aVersionMajor, y4aVersionMinor, y4aVersionPatch, git.revCount].join(".") + y4aVersionSuffix
    y4aVersionCode = y4aVersionMajor * 1000000 + y4aVersionMinor * 1000 + y4aVersionPatch
    y4aVersionName = "${y4aVersionString} (${y4aCodeName} ${y4aBuildDate} ${y4aRevision})"
}

android {
    namespace 'shibafu.yukari'
    compileSdkVersion 33
    buildToolsVersion '33.0.0'

    defaultConfig {
        applicationId "shibafu.yukari"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode y4aVersionCode
        versionName y4aVersionName

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Android Gradle Plugin v0.7.0でビルドが通らない問題の回避
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        resources {
            excludes += ['LICENSE-EDL-1.0.txt', 'LICENSE-EPL-1.0.txt', 'META-INF/LICENSE.txt', 'META-INF/services/javax.annotation.processing.Processor']
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        ignore 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        def releaseStoreFile = System.getenv('SIGNING_STORE_FILE')
        if (releaseStoreFile != null && releaseStoreFile != '' && file(releaseStoreFile).exists()) {
            println "[SigningConfigs] Use keystore file for release build: ${releaseStoreFile}"

            release {
                storeFile file(releaseStoreFile)
                storePassword System.getenv('SIGNING_STORE_PASSWORD')
                keyAlias System.getenv('SIGNING_KEY_ALIAS')
                keyPassword System.getenv('SIGNING_KEY_PASSWORD')
            }
        } else {
            println "[SigningConfigs] SIGNING_STORE_FILE is not defined."
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
        }
    }
    flavorDimensions "stage"
    productFlavors {
        stable {
            dimension "stage"
        }
        alpha {
            dimension "stage"
            applicationIdSuffix ".alpha"
        }
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "applicationId", variant.applicationId
    }
    kotlinOptions {
        apiVersion = '1.9'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // Core library desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Support Library
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'

    def lifecycle_version = '2.5.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Play Services
    implementation "com.google.android.gms:play-services-basement:${playServicesVersion}"

    // UI追加
    implementation 'com.larswerkman:HoloColorPicker:1.4'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'com.github.shibafu528:GalleryMultiPicker:3.0.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'

    // Twitter4J
    implementation "org.twitter4j:twitter4j-core:${twitter4jVersion}"

    // Mastodon
    implementation "com.github.shibafu528.mastodon4j:mastodon4j:${mastodon4jVersion}"
    implementation project(':api:mastodon-ws')

    // パーサ
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'com.twitter:twitter-text:1.13.0'

    // Annotation Processor
    implementation 'com.github.permissions-dispatcher:permissionsdispatcher:4.9.2'
    kapt 'com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2'
    implementation project(':yukari-processor-annotation')
    kapt project(':yukari-processor')

    // Deploygate SDK
    alphaImplementation 'com.deploygate:sdk:4.1.0'

    // いろいろ
    implementation project(':gl-helpers')
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.google.guava:guava:22.0-android'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava' // TODO: com.google.guava:guava:27.0-android 以上に更新したら削除する
    implementation 'org.eclipse.collections:eclipse-collections-api:7.1.0'
    implementation 'org.eclipse.collections:eclipse-collections:7.1.0'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0' // androidx.preferenceとバージョンを合わせる必要あり
    implementation fileTree(dir: './libs', includes: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.mockito:mockito-core:2.21.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    testImplementation "org.robolectric:robolectric:4.9.2"
}

task showVersion {
    println "VersionCode: " + y4aVersionCode
    println "VersionName: " + y4aVersionName
}
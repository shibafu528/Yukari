apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://twitter4j.org/maven2"
    }
    // https://github.com/settings/tokens/new?scopes=read:packages から
    // `read:packages` 権限の付いたPersonal access tokenを発行して、
    // local.propertiesに下記の通りに追記する。
    // ----
    // gpr.user=GitHubユーザー名
    // gpr.key=発行したアクセストークン
    maven {
        url = uri("https://maven.pkg.github.com/shibafu528/yukari-exvoice")
        credentials {
            try {
                def properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                username = properties.getProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = properties.getProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            } catch (FileNotFoundException) {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

class GitUtil {
    def directory

    def getRevHash() {
        def proc = ["git", "rev-parse", "HEAD"].execute(null, directory)
        proc.waitFor()
        return proc.inputStream.readLines().first()
    }

    def getRevCount() {
        def proc = ["git", "rev-list", "--count", "HEAD"].execute(null, directory)
        proc.waitFor()
        return proc.inputStream.readLines().first()
    }

    def isDirty() {
        def proc = ["git", "status", "--porcelain"].execute(null, directory)
        proc.waitFor()
        return proc.inputStream.readLines().size() > 0
    }
}

ext {
    playServicesVersion = '17.6.0'
    twitter4jVersion = '4.0.7'
    mastodon4jVersion = '43b77f366515641a4569c2cfffef57301259c5a0'
    leakCanaryVersion = '1.5.4'
    exvoiceVersion = '20220503.124509'

    y4aVersionMajor = 3
    y4aVersionMinor = 1
    y4aVersionPatch = 2
    y4aVersionSuffix = ''
    y4aCodeName = 'mirage'

    git = new GitUtil(directory: rootProject.projectDir)
    y4aRevision = git.revHash.substring(0, 7)
    if (git.isDirty()) {
        y4aRevision += "*"
    }
    y4aBuildDate = new Date().format("yyMMdd")
    y4aVersionString = [y4aVersionMajor, y4aVersionMinor, y4aVersionPatch, git.revCount].join(".") + y4aVersionSuffix
    y4aVersionCode = y4aVersionMajor * 1000000 + y4aVersionMinor * 1000 + y4aVersionPatch
    y4aVersionName = "${y4aVersionString} (${y4aCodeName} ${y4aBuildDate} ${y4aRevision})"
}

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "shibafu.yukari"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode y4aVersionCode
        versionName y4aVersionName

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Android Gradle Plugin v0.7.0でビルドが通らない問題の回避
    // http://stackoverflow.com/questions/20673888/duplicate-files-copied-android-studio-0-4-0
    packagingOptions {
        resources {
            excludes += ['LICENSE-EDL-1.0.txt', 'LICENSE-EPL-1.0.txt', 'META-INF/LICENSE.txt', 'META-INF/services/javax.annotation.processing.Processor']
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        ignore 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        def releaseStoreFile = System.getenv('SIGNING_STORE_FILE')
        if (releaseStoreFile != null && releaseStoreFile != '' && file(releaseStoreFile).exists()) {
            println "[SigningConfigs] Use keystore file for release build: ${releaseStoreFile}"

            release {
                storeFile file(releaseStoreFile)
                storePassword System.getenv('SIGNING_STORE_PASSWORD')
                keyAlias System.getenv('SIGNING_KEY_ALIAS')
                keyPassword System.getenv('SIGNING_KEY_PASSWORD')
            }
        } else {
            println "[SigningConfigs] SIGNING_STORE_FILE is not defined."
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
        }
    }
    flavorDimensions "stage"
    productFlavors {
        stable {
            dimension "stage"
        }
        alpha {
            dimension "stage"
            applicationIdSuffix ".alpha"
        }
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "applicationId", variant.applicationId
    }
    kotlinOptions {
        apiVersion = '1.3'
    }
}

dependencies {
    // Core library desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Support Library
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Play Services
    implementation "com.google.android.gms:play-services-basement:${playServicesVersion}"

    // UI追加
    implementation 'com.larswerkman:HoloColorPicker:1.4'
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
//    implementation project(':GalleryMultiPicker:library')
    implementation 'com.github.shibafu528:GalleryMultiPicker:a78120ceb0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

    // Twitter4J
    implementation "org.twitter4j:twitter4j-core:${twitter4jVersion}"
    implementation "org.twitter4j:twitter4j-stream:${twitter4jVersion}"
    implementation "org.twitter4j:twitter4j-http2-support:${twitter4jVersion}"

    // Mastodon
    implementation "com.github.shibafu528.mastodon4j:mastodon4j:${mastodon4jVersion}"

    // パーサ
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'org.jsoup:jsoup:1.8.3'
    implementation 'com.google.zxing:javase:3.2.1', {
        exclude group: 'com.beust', module: 'jcommander'
    }
    implementation 'com.twitter:twitter-text:1.13.0'

    // 通信
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'

    // プラグインシステム
    if (getProperty("yukari.exvoice.build").toBoolean()) {
        implementation project(':yukari-exvoice')
    } else {
        implementation "info.shibafu528.yukari:yukari-exvoice:$exvoiceVersion"
    }

    // Annotation Processor
    implementation 'com.jakewharton:butterknife:10.0.0'
    kapt 'com.jakewharton:butterknife-compiler:10.0.0'
    implementation "com.github.hotchemi:permissionsdispatcher:3.3.1", {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    kapt "com.github.hotchemi:permissionsdispatcher-processor:3.3.1"
    implementation project(':yukari-processor-runtime')
    kapt project(':yukari-processor')

    // デバッグ用
    debugImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Deploygate SDK
    alphaImplementation 'com.deploygate:sdk:4.1.0'

    // いろいろ
    implementation project(':gl-helpers')
    implementation 'com.google.guava:guava:22.0-android'
    implementation 'org.eclipse.collections:eclipse-collections-api:7.1.0'
    implementation 'org.eclipse.collections:eclipse-collections:7.1.0'
    implementation 'com.takisoft.preferencex:preferencex:1.0.0'
    implementation fileTree(dir: './libs', includes: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.mockito:mockito-core:2.21.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    testImplementation "org.robolectric:robolectric:4.8.1"
}

// Kotlin
buildscript {
    ext.kotlin_version = '1.6.10'
    ext.kotlin_coroutines_version = '1.6.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

task showVersion {
    println "VersionCode: " + y4aVersionCode
    println "VersionName: " + y4aVersionName
}